/*
 * Master Gradle build script
 *
 * Depends on bndPlugin property set by settings.gradle.
 * and bnd_* values from gradle.properties.
 */

import aQute.bnd.build.Workspace
import aQute.bnd.osgi.Constants

/* Add bnd gradle plugin as a script dependency */
buildscript {
  repositories {
       mavenCentral()
  }
  dependencies {
    classpath bndPlugin
    classpath 'junit:junit:4.+'
    classpath 'info.solidsoft.gradle.pitest:gradle-pitest-plugin:1.1.10'
  }
}

/* Initialize the bnd workspace */
Workspace.setDriver(Constants.BNDDRIVER_GRADLE)
Workspace.addGestalt(Constants.GESTALT_BATCH, null)
ext.bndWorkspace = new Workspace(rootDir, bnd_cnf)
if (bndWorkspace == null) {
  throw new GradleException("Unable to load workspace ${rootDir}/${bnd_cnf}")
}

ext.cnf = rootProject.project(bnd_cnf)

/* Configure the subprojects */
subprojects { p->
  apply plugin: 'java'

  def bndProject = bndWorkspace.getProject(name)
  if (bndProject != null) {
   plugins.apply 'biz.aQute.bnd'
   sourceSets.main.java.srcDirs = ['src', 'generatedsrc']
   apply plugin: 'info.solidsoft.pitest'

   pitest {
        targetClasses = ['com.xeland.*']
        threads = 4
        outputFormats = ['XML', 'HTML']
   }

  }
}
